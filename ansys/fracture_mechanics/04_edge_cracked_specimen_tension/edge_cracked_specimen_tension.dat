/BATCH
/FILNAME, edge_cracked_specimen_tension, 1
/TITLE, edge_cracked_specimen_tension
!
!
*AFUN, DEG
bic = 1.0
hic = 4.0*bic
hic1 = 3.0*bic
tic = 0.2*bic
lic = 0.4*bic
dic = 0.65*bic
zic = 0.5*tic
!
ric = 0.05*lic
aic = 8*ric
nic = 4
x1 = lic-aic/2
x2 = lic-ric
x3 = lic
x4 = lic+ric
x5 = lic+aic/2
x6 = bic
x7 = bic/2
y1 = ric
y2 = aic/2
y3 = hic1/2
y4 = hic/2
alfa = asin(aic/dic)
alpha = 0
n2 = 2*NINT((x6-x5)/ric/6)
n1 = n2
n3 = NINT((y3-y2)/ric/4)
n4 = n3+n1
n5 = n1/2
!
eic = 2.105e11
nuic = 0.3
!
p0 = 1.0e8
fy1 = tic*dic*p0
!
LOCAL, 11, 1, x7, y3, 0.0, 0.0
LOCAL, 12, 0, x7, y3, 0.0, 0.0
CSYS,0
!
!
/PREP7
ET, 1, MESH200, 7
ET, 2, SOLID95
ET, 3, MASS21, , , 
R, 1
R, 2, 1E-3, 1E-3, 1E-3, 1E-3, 1E-3, 1E-3
!
MP, EX, 1, eic
MP, NUXY, 1, nuic
!
/PNUM, KP, 1
K, 1, 0.0, 0.0
K, 2, x1, 0.0
K, 3, x2, 0.0
K, 4, x3, 0.0
K, 5, x4, 0.0
K, 6, x5, 0.0
K, 7, x6, 0.0
K, 8, x3, y1
K, 9, 0.0, y2
K, 10, x1, y2
K, 11, x3, y2
K, 12, x5, y2
K, 13, x6, y2
K, 14, x7-dic*sin(alfa)/2, y3-dic*cos(alfa)/2
K, 15, x7, y3-dic/2
K, 16, x7+dic*sin(alfa)/2, y3-dic*cos(alfa)/2
K, 17, 0.0, y3
K, 18, x7-dic/2, y3
K, 19, x7+dic/2, y3
K, 20, x6, y3
K, 21, 0.0, y4
K, 22, x7, y4
K, 23, x6, y4
K, 24, x7, y3+dic/2
K, 25, x7, y3
KPLOT
!
/PNUM, LINE, 1
L, 1, 2, n1
L, 2, 3, 4, 1/2.5
L, 3, 4
L, 4, 5
L, 5, 6, 4, 2.5
L, 6, 7, n2
LARC, 3, 8, 4, ric
LESIZE, 7, , , nic
L, 4, 8
LARC, 8, 5, 4, ric
LESIZE, 9, , , nic
L, 1, 9, 2
L, 2, 10, 2
L, 8, 11, 4, 2.5
L, 6, 12, 2
L, 7, 13, 2
L, 9, 10, n1
L, 10, 11, 2
L, 11, 12, 2
L, 12, 13, n2
L, 9, 17, n3
L, 10, 14, n4
L, 11, 15, n4
L, 12, 16, n4
L, 13, 20, n3
L, 17, 18, n1
L, 19, 20, n1
L, 17, 21, n5
L, 24, 22, n1
L, 20, 23, n5
L, 21, 22, n5
L, 22, 23, n5
LARC, 18, 14, 25, dic/2
LESIZE, 31, , , n1
LARC, 14, 15, 25, dic/2
LESIZE, 32, , , 2
LARC, 15, 16, 25, dic/2
LESIZE, 33, , , 2
LARC, 16, 19, 25, dic/2
LESIZE, 34, , , n1
LARC, 19, 24, 25, dic/2
LESIZE, 35, , , n1
LARC, 24, 18, 25, dic/2
LESIZE, 36, , , n1
LPLOT
!
/PNUM, AREA, 1
AL, 1, 11, 15, 10
AL, 2, 7, 12, 16, 11
AL, 3, 8, 7
AL, 4, 9, 8
AL, 5, 13, 17, 12, 9
AL, 6, 14, 18, 13
AL, 15, 20, 31, 24, 19
AL, 16, 21, 32, 20
AL, 17, 22, 33, 21
AL, 18, 23, 25, 34, 22
AL, 24, 36, 27, 29, 26
AL, 25, 28, 30, 27, 35
APLOT
SAVE
!
TYPE, 1
MAT, 1
MSHAPE, 0, 2D
MSHKEY, 0
delr = ric/4
KSCON, 4, delr, 1, nic
AMESH, 3, 4, 1
MSHKEY, 1
AMAP, 2, 8, 11, 2, 3
AMAP, 5, 5, 6, 11, 8
AMESH, 1, 6, 5
AMESH, 8, 9, 1
AMAP, 11, 24, 22, 17, 18
AMAP, 12, 19, 20, 22, 24
VEXT, 1, 12, 1, , , tic, , , 
LCCAT, 19, 24
LCCAT, 23, 25
AMESH, 7
AMESH, 10
SAVE
!
TYPE, 2
nz = 8
EXTOPT, ACLEAR, 1
EXTOPT, ESIZE, nz
ESIZE, , nz
VSWEEP, ALL, , , 
SAVE
!
CSYS, 11
*GET, NOD_1, NODE, , NUM, MAX
nrig1 = NOD_1+1
N, nrig1, 0.0, 0.0, 0.0
TYPE, 3
REAL, 2
E, nrig1
NSEL, S, NODE, , nrig1
DOF, ROTX, ROTY, ROTZ
ALLSEL
NSEL, S, LOC, X, dic/2
NSEL, A, NODE, , nrig1
CM, rigid, NODE
CERIG, nrig1, ALL, UX, UY
ALLSEL
CSYS, 0
!
CSYS, 0
NSEL, S, LOC, Y, 0.0
NSEL, R, LOC, X, x3, x6
D, ALL, UY, 0.0
ALLSEL
NSEL, S, LOC, Z, 0.0
D, ALL, UZ, 0.0
ALLSEL
F, nrig1, FY, fy1
D, nrig1, UX, 0.0, , , , UZ, , 
D, ALL, UZ, 0.0
SAVE
FINISH
!
!
/SOLU
ANTYPE, STATIC
EQSLV, SPARSE
SAVE
SOLVE
FINISH
!
!
/POST1
SET, LAST
CSYS, 0
ni = NODE(x3, 0.0, zic)
nj = NODE(x2, 0.0, zic)
nk = NODE(x1, 0.0, zic)
PATH, ki, 3, , 48
PPATH, 1, ni
PPATH, 2, nj
PPATH, 3, nk
KCALC, 0, 1, 0
*GET, KI1, KCALC, , K, 1
*GET, KI2, KCALC, , K, 2
*GET, KI3, KCALC, , K, 3
nji = NODE(x5, 0.0, zic)
njj = NODE(x5, y1, zic)
njk = NODE(x1, y1, zic)
njl = NODE(x1, 0.0, zic)
eic1 = eic/(1-(nuic*nuic))
j_f_1, nji, njj, njk, njl
KI1J = SQRT(eic1*JINT)
!
lr = x3/x6
yr = SQRT(lr)
yr = yr*(1.99-0.41*lr+18.7*(lr**2)-38.5*(lr**3)+53.8*(lr**4))
pb = 2*fy1
KI4 = pb*yr/(2*tic)/SQRT(x6)
/DSCALE, 1, 100
PLNSOL, U, SUM
PLNSOL, S, EQV
FINISH
