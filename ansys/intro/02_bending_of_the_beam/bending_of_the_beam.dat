/batch
/title,bending_of_the_beam
/prep7
/show,bending_of_the_beam,grph
et,1,beam3
keyopt,1,9,9
r,1,2.89e-3,2.03e-5,0.2
mp,ex,1,2e11
mp,nuxy,1,0.3
mp,dens,1,7800
n_el=3
abeam=2
n,1
n,2,abeam
n,3,2*abeam
n,4,3*abeam
e,1,2
e,2,3
e,3,4
save
finish

/solu
antype,static
d,1,uy
d,4,ux
d,4,uy
d,4,rotz
sfbeam,1,,pres,2e4
f,2,mz,-2e4
f,3,fy,3e4
acel,0,9.8,0
allsel,all
solve
finish

/post1
set,1,1
prrsol
/pbc,u,,1
/pbc,f,,1
/pbc,cp,,1
/pbc,m,,1
/psf,pres,norm,1
/pnum,node,1
eplot
/pnum,node,0
/pnum,elem,1
/number,1
eplot

*dim,x_coord,array,n_el*11
*dim,q_y,array,n_el*11
*dim,m_z,array,n_el*11
*dim,u_y,array,n_el*11
*dim,sigma,array,n_el*11
/output,bending_of_the_beam,res
prrsol
/com,
/nopr
*vwrite
(5x,'x',8x,'q_y',10x,'M_z',10x,'u_y',10x,'S_yy')
*vwrite
(1x,'-----------------------------------------------')
*do,i,1,n_el
n1_node=nelem(i,1)
n2_node=nelem(i,2)
nx1_node=nx(n1_node)
nx2_node=nx(n2_node)
l_elem=nx2_node-nx1_node
hx=l_elem/10
uy1=uy(n1_node)
uy2=uy(n2_node)
tz1=rotz(n1_node)
tz2=rotz(n2_node)
esel,s,elem,,i,i
/gopr
*vwrite,i
(/20x,'     elem n=',f2.0)
/nopr
*do,ii,1,11
j=ii+(i-1)*11
xx=nx1_node+hx*(ii-1)
x_coord(j)=xx
ksi=(ii-1)*0.2-1
uu=0.5*(uy1+uy2+(uy2-uy1)*ksi*(3-ksi*ksi)*0.5)
uu=uu+0.125*l_elem*(tz1*(1-ksi)-tz2*(1+ksi))*(1-ksi*ksi)
u_y(j)=uu
etable,sig,ls,ii*3-1
etable,qq_y,smisc,ii*6-4
etable,mm_z,smisc,ii*6
*get,ss,elem,i,etab,sig
*get,qq,elem,i,etab,qq_y
*get,mm,elem,i,etab,mm_z
qq=-qq
sigma(j)=ss
q_y(j)=qq
m_z(j)=mm
/gopr
*vwrite,xx,qq,mm,uu,ss
(1x,f7.1,3x,e10.4,3x,e10.4,3x,e10.4,3x,e10.4)
/nopr
*enddo
*enddo
/output
esel,all
finish

/post26
store,alloc,n_el*11
/xrang,nx(nelem(1,1)),nx(nelem(n_el,2))
/axlab,x,x-coordinate
/color,curve,whit,1,6
/color,grid,whit
/plopts,info,off
/plopts,frame,off
grid,1
vput,x_coord(1),1
vput,q_y(1),2
vput,m_z(1),3
vput,u_y(1),4
vput,sigma(1),5
xvar,1
/output,bending_of_the_beam,res,,append
/com,**************************
/com, vari 2 - q_y
/com, vari 3 - m_z
/com, vari 4 - u_y
/com, vari 5 - sigma_yy
/com, time - x-coordinate
/com,**************************
extrem,2,5
/output
/window,1,ltop
/axlab,y,sh force q_y
plvar, 2
/window,1,off
/noerase
/window,2,rtop
/axlab,y,bend mom m_z
plvar,3
/window,2,off
/window,3,lbot
/axlab,y,displ u_y
plvar,4
/window,3,off
/window,4,rbot
/axlab,y,bend str s_y
plvar,5
finish
/exit
