/BATCH
/FILNAME, plastic_deformation_heat
/TITLE, plastic_deformation_heat
!
!
/PREP7
ET, 1, PLANE223, 11, 0, 1
KEYOPT, 1, 9, 1
MP, DENS, 1, 7850
MPTEMP, 1, 20, 100, 200
MPDATA, EX, 1, 1, 2.08E11, 1.96E11, 1.89E11
MPDATA, PRXY, 1, 1, 0.3, 0.3, 0.3
TB, BISO, 1, 3, 2
TBTEMP, 20
TBDATA, 1, 2.25E8, 1.452E9
TBTEMP, 100
TBDATA, 1, 2.16E8, 1.156E9
TBTEMP, 200
TBDATA, 1, 1.72E8, 1.096E9
MPDATA, ALPX, 1, 1, 1.51E-5, 1.835E-5, 2.015E-5
MPDATA, REFT, 1, 1, 20
MPDATA, KXX, 1, 1, 11.8, 13.8, 16
MPDATA, C, 1, 1, 470, 970, 1980
MPDATA, QRATE, 1, 1, 0.8, 0.85, 0.95
RECTNG, 0, 9E-3, 0, 45E-3
PCIRC, 5E-2, , 0, 360
AGEN, 1, 2, , , 54E-3, , , , , 1
ASBA, 1, 2
LDRAG, 9, , , , , , 3
ASBL, 3, 1
!
LSEL, S, LOC, Y, 5E-3, 15E-3
LESIZE, ALL, , , 80, 4, , , , 0
LSEL, S, LOC, Y, 25E-3, 40E-3
LESIZE, ALL, , , 40, 1, , , , 0
LSEL, S, LOC, X, 4.5E-3
LSEL, A, LOC, Y, 0
LESIZE, ALL, , , 16, 1, , , , 0
ALLSEL
MSHKEY, 1
AMESH, ALL
!
LSEL, S, LOC, Y, 0
DL, ALL, , UY, 0
LSEL, S, LOC, Y, 45E-3
DL, ALL, , UY, 7.5E-3
TUNIF, 20
ALLSEL
FINISH
!
!
/SOLU
ANTYPE, 4
TRNOPT, FULL
NLGEOM, 1
NSUBST, 500, 10000, 50
OUTRES, ALL, ALL
KBC, 0
TIME, 1.5
SOLVE
FINISH
!
!
/POST1
/PLOPTS, MINM, 0
/PLOPTS, DATE, 0
/TRIAD, OFF
/EXPAND, 27, AXIS, , , 10, , 2, RECT, HALF, , 0.00001
SET, , , , , 0.003
PLNSOL, S, EQV
SET, , , , , 0.967
PLNSOL, S, EQV
SET, , , , , 1.5
PLNSOL, S, EQV
!
PLNSOL, TEMP
ANTIME, 150, 0.04, , 1, 2, 0, 1.5
FINISH
!
!
/POST26
NSOL, 2, NODE(0,0,0), TEMP, , TEMP_2
XVAR, 1
PLVAR, 2
FINISH
