/BATCH
/FILNAME, wheel_frictional_heating
/TITLE, wheel_frictional_heating
!
!
/PREP7
pi = ACOS(-1)
ET, 1, SOLID226, 11
KEYOPT, 1, 9, 1
ET, 2, TARGE170
ET, 3, CONTA174, 1, , , , 3
KEYOPT, 3, 9, 1
KEYOPT, 3, 10, 2
ET, 10, MESH200, 6
ET, 4, 174, 1, 2, , 1
ET, 5, 170, , 1, , 0
KEYOPT, 4, 12, 5
ET, 6, 174, 1, 2, , 1
ET, 7, 170, , 1, , 0
KEYOPT, 6, 12, 5
MP, EX, 1, 2E11
MP, PRXY, 1, 0.3
MP, DENS, 1, 7850
MP, ALPX, 1, 12.5E-6
MP, REFT, 1, 20
MP, KXX, 1, 47
MP, C, 1, 470
MP, EX, 2, 1.5E9
MP, PRXY, 2, 0.35
MP, DENS, 2, 3100
MP, KXX, 2, 3
MP, C, 2, 1100
MP, MU, 3, 0.45
!
R, 1
R, 2
R, 3
RMORE
RMORE, , 80, 1, , , 0.5
R, 4
R, 6
PCIRC, 65E-3, , 0, 90
K, , 17E-3
k1 = _RETURN
KGEN, 2, k1, , , 3E-2*3**0.5, 3E-2
KGEN, 2, k1, , , , 7E-2
L, k1, k1+1
L, k1, k1+2
ASBL, ALL, ALL
ASEL, S, LOC, X, 0, 17E-3
ADELE, ALL, , , 1
ASEL, S, LOC, Y, 0, 25E-3
ADELE, ALL, , , 1
ALLSEL
ADELE, ALL, , , 0
LSEL, S, LOC, X, 17E-3
*GET, l1, LINE, 0, NUM, MIN
LSEL, S, LOC, Y, 0, 25E-3
*GET, l2, LINE, 0, NUM, MIN
ALLSEL
LSEL, U, LINE, , l1
LSEL, U, LINE, , l2
*GET, l3, LINE, 0, NUM, MIN
ALLSEL
LFILLT, l1, l2, 15E-3
l4 = _RETURN
LFILLT, l1, l3, 1E-2
l5 = _RETURN
LFILLT, l2, l3, 1E-2
l6 = _RETURN
AL, l4, l1, l5, l3, l6, l2
ASEL, NONE
PCIRC, 3E-3, , 0, 360
AGEN, 1, ALL, , , 24E-3, 0, 0, , , 1
ASEL, NONE
PCIRC, 2.5E-3, , 0, 360
AGEN, 1, ALL, , , 7E-2, , , , , 1
AGEN, 2, ALL, , , 5E-3
CSYS, 1
ASEL, S, LOC, X, 21E-3, 27E-3
AGEN, 1, ALL, , , , 7.5, , , , 1
ASEL, S, LOC, X, 68E-3, 72E-3
AGEN, 1, ALL, , , , 5, , , , 1
ASEL, S, LOC, X, 0, 65E-3
AGEN, 6, ALL, , , , 60
ASEL, S, LOC, X, 65E-3, 8E-2
AGEN, 36, ALL, , , , 10
ALLSEL
CM, subtr, AREA
ASEL, NONE
PCIRC, 17E-3, 8E-2, 0, 360
*GET, a1, AREA, 0, NUM, MIN
ALLSEL
ASBA, a1, subtr
*GET, a1, AREA, 0, NUM, MIN
VOFFST, a1, 1E-3
ASEL, S, LOC, Z, 0
TYPE, 10
ESIZE, 1.25E-3
MSHMID, 2
AMESH, ALL
ALLSEL
TYPE, 1
MAT, 1
REAL, 1
VSWEEP, ALL
ACLEAR, ALL
*GET, na, NODE, 0, NUM, MAX
na = na+1
MAT, 1
REAL, 4
TYPE, 5
N, na, 0, 0, 0
TSHAP, PILO
E, na
TSHAP
ASEL, S, LOC, X, 21E-3, 27E-3
NSLA, S, 1
ESLN, S
TYPE, 4
ESURF
VSEL, NONE
ASEL, NONE
PCIRC, 1E-2, , 0, 90
AGEN, 4, ALL, , , , 90
AGEN, 1, ALL, , , , , 1E-3, , , 1
AGLUE, ALL
CSYS, 0
AGEN, 1, ALL, , , 72.5E-3, , , , , 1
VEXT, ALL, , , , , 4E-3
TYPE, 10
ASEL, R, LOC, Z, 1E-3
MSHKEY, 1
ESIZE, 2E-3
AMESH, ALL
ASLV, S
TYPE, 1
MAT, 2
REAL, 2
VSWEEP, ALL
ACLEAR, ALL
*GET, nb, NODE, 0, NUM, MAX
nb = nb+1
MAT, 2
REAL, 6
TYPE, 7
N, nb, 72.5E-3, 0, 1E-2
TSHAP, PILO
E, nb
TSHAP
ASEL, R, LOC, Z, 5E-3
NSLA, S, 1
ESLN, S
TYPE, 6
ESURF
VSEL, S, LOC, Z, 0, 1E-3
ASLV, S
ASEL, R, LOC, Z, 1E-3
NSLA, S, 1
ESLN, S
TYPE, 2
REAL, 3
MAT, 3
ESURF
VSEL, S, LOC, Z, 1E-3, 5E-3
ASLV, S
ASEL, R, LOC, Z, 1E-3
NSLA, S, 1
ESLN, S
TYPE, 3
REAL, 3
MAT, 3
ESURF
*DIM, ROTTABLE, TABLE, 2002, , , TIME
ROTTABLE(0,1) = 0
ROTTABLE(1,0) = 0.01
ROTTABLE(2,0) = 0.11
ROTTABLE(1,1) = 0
ROTTABLE(2,1) = 0.7*pi
*DO, i, 0.001, 2, 0.001
ROTTABLE(i*1000+2,0) = i+0.11
ROTTABLE(i*1000+2,1) = 0.7*pi+(7*i-3.5*i*i/2)*pi
*ENDDO
*DIM, UTABLE, TABLE, 5, , , TIME
UTABLE(0,1) = 0
UTABLE(1,0) = 0.01
UTABLE(2,0) = 0.1
UTABLE(3,0) = 0.11
UTABLE(4,0) = 2.11
UTABLE(5,0) = 2.12
UTABLE(1,1) = 5E-4
UTABLE(2,1) = 1E-5
UTABLE(3,1) = -22.8E-6
UTABLE(4,1) = -22.8E-6
UTABLE(5,1) = 5E-4
ALLSEL
TUNIF, 20
VSEL, S, LOC, Z, 0, 1E-3
ASLV, S
ASEL, U, LOC, Z, 0
SFA, ALL, 1, CONV, 15, 20
VSEL, S, LOC, Z, 1E-3, 5E-3
ASLV, S
ASEL, R, EXT
ASEL, U, LOC, Z, 1E-3
ASEL, U, LOC, Z, 5E-3
SFA, ALL, 1, CONV, 10, 20
ASLV, S
ASEL, R, LOC, Z, 5E-3
SFA, ALL, 1, CONV, 150, 20
ASEL, S, LOC, Z, 0
DA, ALL, UZ, 0
SFA, ALL, 1, HFLUX, 0
ALLSEL
FINISH
!
!
